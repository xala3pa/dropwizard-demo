group 'com.codurance'
version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'eu.appsatori.fatjar'
apply plugin: 'application'

// The main class of the application
mainClassName = 'com.codurance.dropwizard.HelloWorldApplication'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'eu.appsatori:gradle-fatjar-plugin:0.3'
    }
}


// Set our project variables
project.ext {
    dropwizardVersion = '0.8.4'
    dropWizardConfig = './codurance.yml'
}

dependencies {
    compile (
            'io.dropwizard:dropwizard-core:0.8.4',
            'io.dropwizard:dropwizard-testing:0.8.4',
            'org.assertj:assertj-core:3.2.0'
    )
    testCompile group: 'junit', name: 'junit', version: '4.11'
}


// Configure the fatJar task
fatJar {
    classifier 'fat'

    // We need to add the main class to the manifest, otherwise the JAR won't start.
    manifest {
        attributes 'Main-Class': mainClassName
    }

    // We have to exclude signature files from the fat JAR, otherwise we'll get invalid signature file digest errors.
    exclude 'META-INF/*.DSA', 'META-INF/*.RSA', 'META-INF/*.SF'
}

// Configure the run task to start the Dropwizard service

run {
    args 'server', dropWizardConfig
}


task stage(dependsOn: ['clean', 'fatJar'])
